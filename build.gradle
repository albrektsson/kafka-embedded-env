buildscript {
    ext.kotlin_version = '1.2.50'
    ext.unboundid_version = '4.0.6'
    ext.zookeeper_version = '3.4.12'
    ext.kafka_version = '1.1.0'
    ext.commons_version = '1.3.2'
    ext.confluent_version = '4.1.1'
    ext.kluent_version = '1.38'
    ext.spek_version = '1.1.5'
    ext.fuel_version = '1.13.0'
    ext.slf4j_version = '1.7.5'
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.50"
    id 'maven-publish'
    id 'org.jmailen.kotlinter' version '1.13.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id "org.jetbrains.dokka" version "0.9.17"
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.11.0'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from dokka
    classifier 'javadoc'
}

test {
    useJUnitPlatform {
        includeEngines 'spek'
    }
}

group 'no.nav'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "http://packages.confluent.io/maven"
    }
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.unboundid:unboundid-ldapsdk:$unboundid_version"
    implementation "org.apache.kafka:kafka_2.12:$kafka_version"
    implementation "org.apache.commons:commons-io:$commons_version"
    implementation ("io.confluent:kafka-schema-registry:$confluent_version") {
        exclude group: 'org.apache.kafka'
    }
    implementation ("io.confluent:kafka-rest:$confluent_version") {
        exclude group: 'org.apache.kafka'
    }
    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "org.jetbrains.spek:spek-api:$spek_version"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
    testImplementation "com.github.kittinunf.fuel:fuel:$fuel_version"
    testRuntimeOnly "org.slf4j:log4j-over-slf4j:$slf4j_version"
    testImplementation 'org.junit.platform:junit-platform-runner:1.2.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'kafka-embedded-env'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'kafka-embedded-env'
                description = 'Simple API for running a Kafka/Confluent environment locally'
                url = 'https://github.com/navikt/kafka-embedded-env'
                withXml {
                    asNode().appendNode('packaging', 'jar')
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        organization = "NAV (Arbeids- og velferdsdirektoratet) - The Norwegian Labour and Welfare Administration"
                        organizationUrl = "https://www.nav.no"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/navikt/kafka-embedded-env.git"
                    developerConnection = "scm:git:git://github.com/navikt/kafka-embedded-env.git"
                    url = "https://github.com/navikt/kafka-embedded-env"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusStaging {
    username = ossrhUsername
    password = ossrhPassword
    packageGroup = 'no.nav'
}
